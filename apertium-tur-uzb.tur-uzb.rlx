DELIMITERS = "<.>" "<!>" "<?>" ;
SOFT-DELIMITERS = "<,>" ;

LIST BOS = (>>>) sent ; # Beginning of sentence
LIST EOS = (<<<) sent ; # End of sentence

LIST N = n ; 
LIST V = v ; 
LIST Prop = np ; 
LIST Pron = prn ; 
LIST Num = num ; 
LIST A = adj ; 
LIST Det = det ; 
LIST Adv = adv ; 
LIST CC = cnjcoo ; 
LIST CS = cnjsub ; 
LIST Interj = ij ; 
LIST Post = post ; 
LIST Cop = cop ; 
LIST Num = num ; 
LIST Poss2 = px2sg ; 
LIST Poss3 = px3sg px3sp ;

LIST Px3Sp = px3sp ; 
LIST Px2Sg = px2sg ; 

LIST Nom = nom ; 
LIST Gen = gen ; 
LIST Abe = abe ; 
LIST Dat = dat ;
LIST Loc = loc ; 

LIST Subst = subst ; 
LIST Attr = attr ; 
LIST Advl = advl ; 

LIST Ant = (np ant);
LIST Cog = (np cog);

LIST Recip = rec ; 
LIST Caus = caus ; 

LIST Ger = ger1 ger2 ger3 ; 
SET FINITE = V - Ger ;

LIST Past = past ; 


LIST Imper = imp ; 

SET PRE-N = Det | Num | Attr | A | Gen | ("-") ; # CC 

SET NOMINAL-HEAD = N | Ger | Subst ; 

SET WORD = N | V | A | Post | Pron | Det | Adv | CC | CS | Interj | Num | ("\?") ;
SET MARK =  (",") | ("\\") | ("\;") ; #"
SET WORDMARK = WORD | MARK ;

SET NPNHA = WORDMARK - PRE-N - Adv ;


SECTION

#
## Remove productive derivations when there are lexicalised alternatives
#

REMOVE V + Recip IF (0 V - Recip) ; 
    ## Samaras liderliğindeki koalisyon, PASOK ve küçük Demokratik Sol tarafından oluşmasına karşın, ...

REMOVE V + Caus IF (0 V - Caus) ; 
    ## ... , yan emekli aylıklarının kaldırılmasını içermesi bekleniyor.

REMOVE N + Abe IF (0 A) ; 
    ## , yaklaşık 2 milyon kişiyi işsiz bırakan ve ekonomiyi yüzde 7 oranında küçülten tasarruf tedbirlerinin devamı yönündeki ısrarına karşı direniyor.

#
## Unlikely readings can be discarded, we can write rules to select these in certain contexts.
#

#UGLY

REMOVE Poss2 IF (0 Poss3 + Loc) (-1 Num);

SELECT Adv IF (0 ("yarın")) ;
    ## Yarın gelir misin?

REMOVE Cog + Px2Sg IF (0 Cog + Gen) ; 
    ## [0]Cinciç'in anısına Belgrad'da binlerce kişi toplandı.

REMOVE Imper IF (NOT 0C Imper) ;
    ## Bunlar, bu hafta içinde dünyanın dört bir yanından İtalyan başkentine [0]akın etmiş olan yaklaşık dört milyon hacı ve yaslı kalabalığa katıldı.


SECTION 

REMOVE Cop IF (NOT 1 EOS) ;
    ## Eczanelerde satılan [0]ilaçlar da bu alanda rağbet görüyor.

REMOVE Pron + Nom IF (0 Det) (1C N) ;
    ## Bunlar, [0]bu hafta içinde dünyanın dört bir yanından İtalyan başkentine akın etmiş olan yaklaşık dört milyon hacı ve yaslı kalabalığa katıldı.

REMOVE Num + Nom IF (1C Num) ; 
    ## Bu araba [0]11,5 milyar avroluk.

REMOVE Attr IF (NOT 1 PRE-N) (NOT 1 NOMINAL-HEAD) ; 
    ## Bu araba 11,5 milyar avroluk.

SELECT Adv IF (0 ("en")) (1 A) ; 
    ## En az 11,5 milyar avroluk ve ...

REMOVE Subst + Nom IF (1* N + Nom BARRIER NPNHA)  ; 
    ## En az 11,5 milyar avroluk ve ...

REMOVE N + Nom IF (0 Attr OR Nom) (1C Nom) ;
    ## Samaras'ın AB-IMF-ECB troykası tarafından talep edilen ve krizi daha da derinleştiren, ...

SELECT Attr (0 Nom) (1C Px3Sp + Nom) ; 
    ## Girit Üniversitesi ekonomi öğretim üyesi ve ...
    
#Construction gen + poss (ataturk'un cumhuriyeti)

SELECT Poss3 IF (-1 Gen) ; 

SELECT Gen IF (1C Poss3) ; 


    

    
### POSTPOSITIONS


"<doğru>" SELECT Post IF (-1 Dat) ;

"<göre>" SELECT Post IF (-1 Dat) ;

"<qarshi>" SELECT Post IF (-1 Dat) ;

"<kadar>" SELECT Post IF (-1 Dat) ;

"<karşı>" SELECT Post IF (-1 Dat) ;
 
"<rağmen>" SELECT Post IF (-1 Dat) ;

"<ait>" SELECT Post IF (-1 Dat) ;

"<dek>" SELECT Post IF (-1 Dat) ;

"<aykırı>" SELECT Post IF (-1 Dat) ;

"<başka>" SELECT Post IF (-1 Abe) ;
 
"<beri>" SELECT Post IF (-1 Abe) ;

"<bu yana>" SELECT Post IF (-1 Abe) ;

"<dolayı>" SELECT Post IF (-1 Abe) ;

"<itibaren>" SELECT Post IF (-1 Abe) ;
 
"<ötürü>" SELECT Post IF (-1 Abe) ;

"<sonra>" SELECT Post IF (-1 Abe) ;

"<yana>" SELECT Post IF (-1 Abe) ;

"<arkasından>" SELECT Post IF (-1 Gen) ;

## WORDS SPECIFIC

"<yıl>" REMOVE Attr ;


# ATTRIBUTIVE ADJ

#If an adjective is right before a finite verb, select its adverbial reading

SELECT Advl IF (1C FINITE) ; 


#If an adjective is right before a numeral + noun it is an adjective for sure

SELECT A IF (1C Num) (2C N) ;

#If an adjective is right before the end of a sentence, we can safely expect it to be an adjective.

REMOVE Advl IF (0 A)(1 EOS)

#If the following item cannot include a copula in its reading, discard subst from the adjective.

REMOVE Subst IF (0 A) (1 EOS) (NOT 1 Cop) ;

REMOVE Attr IF (0 N) (-1 A) ; 

#VERBS

#Select FINITE FORM (in this case past) if it is the last word of the sentence

SELECT Past IF (1 EOS) ; 

#If we have a form which is both present as N1 or derivative gerund, select N1

 SELECT N IF (0 N) (0 Ger)


#Remove V + V reading

REMOVE V IF (1 FINITE) (2 EOS) ; 


